[ present_results
    "The sum is: " print . cr
    swap -
    "The time difference is: " print . "ns" print cr
]

[ sl-helper 
   # ( sum max cur )
   rot       # ( max cur sum )
   over      # ( max cur sum cur )
   +         # ( max cur sum1 )
   rot rot   # ( cur sum1 max -- sum1 max cur )
]   


[ test_skiploop
    clock           # mark clock start
    0 20000000 0    # sum, max, current

    # start loop here:
    2dup =
    5 * skip
    
    # false:
    1 +             # increment current index
    sl-helper       # stack magic to accumulate
    -10 skip        # loop back to '2dup'

    # true, exit:
    2drop           # remove max, current
    clock
    swap
]     

[ recur-loop
    1 +        # sum cur'
    swap       # cur' sum
    over       # cur' sum cur'
    +          # cur' sum'
    swap       # sum' cur'
    dup        # sum' cur' cur'
    20000000 <>
    2 * skip
    
    # false
    1 skip

    # true    
  recur-loop
]

[ test_recur
    clock
    0 0 recur-loop   # clock sum cur func
    clock            # clock sum cur clock'
    swap             # clock sum clock' cur
    drop swap        # clock clock' sum
]

[ testfor
    clock 0
    20000001 0 1 for
      i +
    next
    clock
    swap
]

[ testdo
    clock 0
    20000001 do
      i +
    redo
    clock
    swap
]

[ testdo-var
    clock
    0 0 !
    20000001 do
      0 @ i + 0 !
    redo
    0 @
    clock
    swap
]

"\nThis will show how various ways of composing loops are more efficient.
In general, go with the simplest case, and avoid logical and stack operations
inside of loops, when you can. Hence, the the first 'manual' loop here is the
worst performer, and the simple 'do' loop at the end is the fastest.
" print 

"\nTesting loop using manual 'skip' loop\n" print
test_skiploop
present_results

"\nTesting loop using recursion\n" print
test_recur
present_results

"\nTesting loop using 'for'\n" print
testfor
present_results

"\nTesting loop using 'do'\n" print
testdo
present_results

"\nTesting loop using 'do' and poking/peeking a variable\n" print
testdo-var
present_results
